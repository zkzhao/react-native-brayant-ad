buildscript {
  repositories {
    google()
    mavenCentral()
  }
  def buildGradleVersion = ext.has('buildGradlePluginVersion') ? ext.get('buildGradlePluginVersion') : '8.1.4'

  dependencies {
    classpath "com.android.tools.build:gradle:$buildGradleVersion"
  }
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties["BrayantAd_" + name]
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["BrayantAd_" + name]).toInteger()
}

static def supportsNamespace() {
  def parsed = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')
  def major = parsed[0].toInteger()
  def minor = parsed[1].toInteger()

  // Namespace support was added in 7.3.0
  return (major == 7 && minor >= 3) || major >= 8
}

android {
  compileSdk 34
  if (supportsNamespace()) {
    namespace "com.brayantad"

    sourceSets {
      main {
        manifest.srcFile "src/main/AndroidManifestNew.xml"
      }
    }
  }

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")

  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}
repositories {
  mavenCentral()
  google()
  maven {
    url 'https://artifact.bytedance.com/repository/pangle'
  }
}


dependencies {
    implementation 'com.pangle.cn:ads-sdk-pro:7.3.0.8'
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    // react-native is provided by the host React Native project
    // When this library is used in a RN project, the host project
    // automatically includes the correct version of react-native
    
    implementation "com.github.bumptech.glide:glide:4.11.0"
}


